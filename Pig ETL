
/* LOAD MY 200K STACK EXCHANGE POSTS (SAVED AS FOUR 50K POSTS FILES) INTO PIG FROM THE HDFS (SAVED IN THE /TOP200 FOLDER) */

/* TAKE MY 4 INPUT FILES AND LOAD THEM INTO PIG USING THE CSVExcelStorage FUNCTION */
/* ALLOW MULTILINE TO ACCOUNT FOR LINE BREAKS WHICH EXIST WITHIN THE BODY FIELD DUE TO THE POST TEXT */
/* SKIPPING MY INPUT HEADER AS I WILL BE LOADING INTO HIVE LATER SO NOT NEEDED FOR THIS STEP */

top200_merged = LOAD '/top200/*.csv' USING org.apache.pig.piggybank.storage.CSVExcelStorage(',','YES_MULTILINE','NOCHANGE','SKIP_INPUT_HEADER') AS(Id:int, PostTypeId:int, AcceptedAnswerId:int, ParentId:int, CreationDate:datetime, DeletionDate:datetime, Score:int, ViewCount:int, Body:chararray, OwnerUserId:int, OwnerDisplayName:chararray, LastEditorUserId:int, LastEditorDisplayName:chararray, LastEditDate:datetime, LastActivityDate:datetime, Title:chararray, Tags:chararray, AnswerCount:int, CommentCount:int, FavoriteCount:int, ClosedDate:datetime, CommunityOwnedDate:datetime);

/* TEST TO ENSURE OF NO DUPLICATION OF RECORDS WITHIN MY 200k POSTS */

top200_merged_distinct = DISTINCT top200_merged;

/* ONLY BRINGING BACK RELEVANT COLUMNS NEEDED TO COMPLETE TO IMPROVE JOB EFFICIENCY */
/* THEREFORE LAODING ONLY ID, SCORE, OWNERUSERID & BODY */

top200_uncleaned_filtered = FOREACH top200_merged_distinct GENERATE Id,Score,OwnerUserId,Body;

/* STEP 1 OF CLEANING PROCESS. FINDING ALL HTML TAGS IN BODY FIELD AND REMOVING THEM */

top200_cleaned_filtered_1 = FOREACH top200_uncleaned_filtered GENERATE Id,Score,OwnerUserId,REPLACE(Body, '<.*?.>', '') AS Body;

/* STEP 2 OF CLEANING PROCESS. FINDING ALL LINE BREAKS IN MY BODY AND REMOVING THEM */

top200_cleaned_filtered_2 = FOREACH top200_cleaned_filtered_1 GENERATE Id,Score,OwnerUserId,REPLACE(Body, '\n', ' ') AS Body;

/* STORING MY CLEANED & FILTERED DATA USING THE CSVExcelStorage FUNCTION */
/* STORING MY FILE AS PIPE DELIMITED AS IT MAY BE OF BENEFIT LATER IN THE TF-IDF SECTION OF THE TASK */
/* SWITCHING TO NO_MULTILINE TO REFLECT MY CLEANED CODE WIHTOUT ANY LINE BREAKS */
/* SKIPPING MY OUTPUT HEADER AS PER ABOVE AS I WILL BE LOADING INTO HIVE LATER SO NOT NEEDED FOR THIS STEP */

STORE top200_cleaned_filtered_2 INTO '/cleaned_data' USING org.apache.pig.piggybank.storage.CSVExcelStorage('|','NO_MULTILINE','NOCHANGE','SKIP_OUTPUT_HEADER');
